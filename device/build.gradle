apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {

    defaultConfig {

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {

        release {

            minifyEnabled false
            // shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    rootProject.file('tools/proguard/rules-proguard.pro')
        }
    }

    lintOptions {

        abortOnError false
    }
}

dependencies {

    api fileTree(dir: 'libs', include: ['*.jar'])

    api project(':data')
    api project(':domain')

    def libs = [
            dagger: [group: 'com.google.dagger', name: 'dagger', version: daggerVersion],
            daggerCompiler: [group: 'com.google.dagger', name: 'dagger-compiler', version: daggerVersion],
            googlePlayServicesLocation: [group: 'com.google.android.gms', name: 'play-services-location', version: googlePlayServicesVersion],
            javaxAnnotation: [group: 'javax.annotation', name: 'jsr250-api', version: javaxAnnotationVersion],
            kotlin: [group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlinVersion],
            kotlinReflect: [group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlinVersion]
    ]

    def testLibs = [
            kotlinUnitTest: [group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit', version: kotlinVersion]
    ]

    def appDependencies = [
            [configuration: 'api', dependency: libs.dagger],
            [configuration: 'kapt', dependency: libs.daggerCompiler],
            [configuration: 'api', dependency: libs.googlePlayServicesLocation],
            [configuration: 'compileOnly', dependency: libs.javaxAnnotation],
            [configuration: 'api', dependency: libs.kotlin],
            [configuration: 'api', dependency: libs.kotlinReflect],

            [configuration: 'testImplementation', dependency: testLibs.kotlinUnitTest]
    ]

    appDependencies.each {
        add(it.configuration, it.dependency, it.options)
    }
}

apply from: "${rootProject.getRootDir()}/android.gradle"